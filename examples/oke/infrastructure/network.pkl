/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@oci/oci.pkl"
import "@oci/core/vcn.pkl"
import "@oci/core/subnet.pkl"
import "@oci/core/internetgateway.pkl"
import "@oci/core/natgateway.pkl"
import "@oci/core/servicegateway.pkl"
import "@oci/core/routetable.pkl"
import "@oci/core/securitylist.pkl"

import "../vars.pkl"

class NetworkResources {
    compartmentId: String
    projectName: String
    serviceLbSubnetCidr: String
    workerNodeSubnetCidr: String
    serviceGatewayServiceId: String
    vcn: vcn.VCN
    securityList: securitylist.SecurityList

    // Internet Gateway - for public subnet access
    hidden internetGateway: internetgateway.InternetGateway = new internetgateway.InternetGateway {
        label = "oke-igw"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        isEnabled = true
        displayName = projectName + "-igw"
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-igw" }
        }
    }

    // NAT Gateway - for private subnet egress to internet
    hidden natGateway: natgateway.NatGateway = new natgateway.NatGateway {
        label = "oke-nat-gw"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        displayName = projectName + "-nat-gw"
        blockTraffic = false
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-nat-gw" }
        }
    }

    // Service Gateway - for OCI services access
    hidden serviceGateway: servicegateway.ServiceGateway = new servicegateway.ServiceGateway {
        label = "oke-svc-gw"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        displayName = projectName + "-svc-gw"
        services {
            new servicegateway.ServiceGatewayService {
                serviceId = serviceGatewayServiceId
            }
        }
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-svc-gw" }
        }
    }

    // Public Route Table - for API endpoint and service LB subnets
    hidden publicRouteTable: routetable.RouteTable = new routetable.RouteTable {
        label = "oke-public-rt"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        displayName = projectName + "-public-rt"
        routeRules {
            new routetable.RouteRule {
                description = "traffic to/from internet"
                destination = "0.0.0.0/0"
                destinationType = "CIDR_BLOCK"
                networkEntityId = internetGateway.res.id
            }
        }
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-public-rt" }
        }
    }

    // Private Route Table - for worker node subnet
    hidden privateRouteTable: routetable.RouteTable = new routetable.RouteTable {
        label = "oke-private-rt"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        displayName = projectName + "-private-rt"
        routeRules {
            new routetable.RouteRule {
                description = "traffic to the internet"
                destination = "0.0.0.0/0"
                destinationType = "CIDR_BLOCK"
                networkEntityId = natGateway.res.id
            }
            new routetable.RouteRule {
                description = "traffic to OCI services"
                destination = "all-ord-services-in-oracle-services-network"
                destinationType = "SERVICE_CIDR_BLOCK"
                networkEntityId = serviceGateway.res.id
            }
        }
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-private-rt" }
        }
    }

    // Service Load Balancer Subnet - for Kubernetes LoadBalancer services (PUBLIC)
    svcLbSubnet: subnet.Subnet = new subnet.Subnet {
        label = "oke-svclb-subnet"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        cidrBlock = serviceLbSubnetCidr
        displayName = projectName + "-svclb-subnet"
        dnsLabel = "okesvclb"
        prohibitPublicIpOnVnic = false
        prohibitInternetIngress = false
        routeTableId = publicRouteTable.res.id
        securityListIds {
            securityList.res.id
        }
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-svclb-subnet" }
            new oci.FreeformTag { key = "OKECluster"; value = projectName }
        }
    }

    // Worker Node Subnet - for OKE node pool worker nodes (PRIVATE)
    workerSubnet: subnet.Subnet = new subnet.Subnet {
        label = "oke-worker-subnet"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        cidrBlock = workerNodeSubnetCidr
        displayName = projectName + "-worker-subnet"
        dnsLabel = "okeworker"
        prohibitPublicIpOnVnic = true      // PRIVATE: No public IPs
        prohibitInternetIngress = true     // PRIVATE: No direct internet ingress
        routeTableId = privateRouteTable.res.id
        securityListIds {
            securityList.res.id
        }
        freeformTags {
            new oci.FreeformTag { key = "Name"; value = projectName + "-worker-subnet" }
            new oci.FreeformTag { key = "OKECluster"; value = projectName }
        }
    }

    hidden resources: Listing<formae.Resource> = new {
        internetGateway
        natGateway
        serviceGateway
        publicRouteTable
        privateRouteTable
        svcLbSubnet
        workerSubnet
    }
}
