/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

import "@formae/formae.pkl"
import "@oci/core/vcn.pkl"
import "@oci/core/subnet.pkl"
import "@oci/containerengine/cluster.pkl"
import "@oci/containerengine/nodepool.pkl"

import "../vars.pkl"

class OkeResources {
    compartmentId: String
    projectName: String
    k8sVersion: String
    vmShape: String
    nodeOcpus: Float
    nodeMemoryGBs: Float
    nodePoolSize: Int
    availabilityDomain: String
    nodeImageId: String
    vcn: vcn.VCN
    svcLbSubnet: subnet.Subnet
    workerSubnet: subnet.Subnet

    hidden okeCluster: cluster.Cluster = new cluster.Cluster {
        label = "oke-cluster"
        compartmentId = outer.compartmentId
        vcnId = vcn.res.id
        kubernetesVersion = k8sVersion
        name = projectName

        // API endpoint uses the service LB subnet (public)
        endpointConfig = new cluster.ClusterEndpointConfig {
            subnetId = svcLbSubnet.res.id
            isPublicIpEnabled = true
        }

        options = new cluster.ClusterCreateOptions {
            // Service load balancers use their own subnet
            serviceLbSubnetIds {
                svcLbSubnet.res.id
            }
            kubernetesNetworkConfig = new cluster.KubernetesNetworkConfig {
                podsCidr = "10.244.0.0/16"
                servicesCidr = "10.96.0.0/16"
            }
        }

        tags {
            new { key = "Name"; value = projectName }
            new { key = "Project"; value = projectName }
        }
    }

    hidden okeNodePool: nodepool.NodePool = new nodepool.NodePool {
        label = "oke-nodepool"
        compartmentId = outer.compartmentId
        clusterId = okeCluster.res.id
        name = projectName + "-nodepool"
        kubernetesVersion = k8sVersion
        nodeShape = vmShape

        nodeShapeConfig = new nodepool.NodeShapeConfig {
            ocpus = nodeOcpus
            memoryInGBs = nodeMemoryGBs
        }

        nodeConfigDetails = new nodepool.NodeConfigDetails {
            size = nodePoolSize
            placementConfigs {
                new nodepool.NodePoolPlacementConfig {

                    //TODO: Fix this so that it doesn't need to be passed in as a property
                    availabilityDomain = vars.props.availabilityDomain.value

                    // Worker nodes use private subnet
                    subnetId = workerSubnet.res.id
                }
            }
        }

        // Required: OKE-optimized node image. See vars.pkl for the image ID
        nodeSourceDetails = new nodepool.NodeSourceViaImageDetails {
            imageId = nodeImageId
            bootVolumeSizeInGBs = 50
        }

        initialNodeLabels {
            new nodepool.NodeLabel {
                key = "environment"
                value = "example"
            }
            new nodepool.NodeLabel {
                key = "nodepool"
                value = "default"
            }
        }

        tags {
            new { key = "Name"; value = projectName + "-nodepool" }
            new { key = "Project"; value = projectName }
        }
    }

    hidden resources: Listing<formae.Resource> = new {
        okeCluster
        okeNodePool
    }
}
